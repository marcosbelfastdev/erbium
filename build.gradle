apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'java'


repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

jar {
    baseName 'erbium'
}

compileJava {
    options.compilerArgs.add("-Xlint:unchecked")
    options.compilerArgs.add("-Xlint:deprecation")
}


ext.seleniumVersion         = "3.141.59"
ext.testNgVersion           = '7.4.0'
project.archivesBaseName    = project.name
project.version             = "1.0.0"

ext.projectGroupId          = "com.github.marcosbelfastdev"
ext.projectArtifactId       = "erbium"
ext.projectLongName         = "Erbium Layer 2"
ext.projectArtefactDir      = "jars/"
ext.projectMainClass        = "org.openqa.selenium.phantomjs.PhantomJSDriver"
ext.projectCreator          = "Marcos Ghiraldelli (ghiraldelli@icloud.com)"

dependencies {
    compile "org.seleniumhq.selenium:selenium-remote-driver:$seleniumVersion"
}

// Forces Java 11 compile source/target
compileJava {
    sourceCompatibility = 1.11
    targetCompatibility = 1.11
}

tasks.withType(JavaExec) {
    classpath = configurations.compile + sourceSets.test.output
    args project.hasProperty("args") ? project.args.split("\\s") : []
}

// Binary Jar
jar {
    destinationDir      = file(projectArtefactDir)
    version             = project.version
    manifest {
        attributes(
                "Implementation-Title"      : projectLongName,
                "Implementation-Version"    : project.version,
                "Gradle-Version"            : project.gradle.gradleVersion,
                "Created-By"                : projectCreator
        )
    }
}

// Source Jar
task sourceJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource

    destinationDir      = file(projectArtefactDir)
    version             = project.version
    manifest {
        attributes(
                "Implementation-Title"      : projectLongName,
                "Implementation-Version"    : project.version,
                "Gradle-Version"            : project.gradle.gradleVersion,
                "Created-By"                : projectCreator
        )
    }
}

// JavaDoc Jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir

    destinationDir      = file(projectArtefactDir)
    version             = project.version
    manifest {
        attributes(
                "Implementation-Title"      : projectLongName,
                "Implementation-Version"    : project.version,
                "Gradle-Version"            : project.gradle.gradleVersion,
                "Created-By"                : projectCreator
        )
    }
}


// Make all Jars
task jars {
    dependsOn jar, sourceJar, javadocJar
}

// Store JavaDoc
task storeJavadoc(type: Sync, dependsOn: javadoc) {
    from project.docsDir
    into "docs"
}

// Artifact-making Tasks
artifacts {
    archives jar
    archives javadocJar
    archives sourceJar
}

// To specify a license in the pom:
install {
    repositories.mavenInstaller {
        pom.project {
            name            "Erbium Layer 2"
            description     """
        Erbium (Layer 2) is a Selenium wrapper intended to provide new functionality
        and make test scripts more robust and fault tolerant.
        It gives flexibility over a variety of driver options, which are enforced
        throughout.
        """
            groupId         projectGroupId
            artifactId      projectArtifactId
            version         project.version
            packaging       "jar"
            url             "https://github.com/marcosbelfastdev/erbium"
            inceptionYear   "2012"

            scm {
                url                     "https://github.com/marcosbelfastdev/erbium"
                connection              "scm:git:git@github.com:marcosbelfastdev/erbium.git"
                developerConnection     "scm:git:git@github.com:marcosbelfastdev/erbium.git"
            }

            licenses {
                license {
                    name            "The BSD 2-Clause License"
                    url             "http://opensource.org/licenses/BSD-2-Clause"
                    distribution    "repo"
                }
            }

            developers {
                developer {
                    id          "marcosbelfastdev"
                    name        "Marcos Ghiraldelli"
                    email       "ghiraldelli@icloud.com"
                    url         "http://www.erbiumframework.org"
                    roles {
                        role    "Owner"
                        role    "Developer"
                    }
                    timezone    "GMT"
                }
            }
        }
    }
}